//@version=5
indicator("Tier 2 Daily Pattern", overlay=true, shorttitle="T2DP")

isDaily = timeframe.isdaily

emaLen10  = 10
emaLen20  = 20
emaLen200 = 200

ema10  = ta.ema(close, emaLen10)
ema20  = ta.ema(close, emaLen20)
ema200 = ta.ema(close, emaLen200)

plot(ema10, title="EMA 10", color=color.blue)
plot(ema20, title="EMA 20", color=color.red)
plot(ema200, title="EMA 200", color=color.orange)

// Ensure labels are only drawn when EMA 10 > EMA 20 and EMA 20 > EMA 200
emaTrendOk = (ema10 > ema20) and (ema20 > ema200)

crosses(emaVal) =>
    (low < emaVal) and (high > emaVal)

var bool restricted        = false
var bool bearishCandidate  = false
var bool justReset         = false
var bool t2Triggered       = false

justReset := false

if restricted
    if (close > open) and (close > ema10)
        restricted := false
        justReset  := true
        bearishCandidate := false

t2Triggered := false
var bool permission = false
if not restricted and not justReset and emaTrendOk
    condA = (close > open) and (crosses(ema10) or crosses(ema20)) and (close > ema10)
    condBearCandidate = (close < open) and (crosses(ema10) or crosses(ema20)) and (close > ema10)
    if condBearCandidate
        bearishCandidate := true
    condBConfirm = bearishCandidate and (close > open) and (close > ema10)
    if condA or condBConfirm
        permission := true
        bearishCandidate := false

if not restricted and not justReset
    if (open < ema20) or (close < ema20)
        restricted := true
        bearishCandidate := false

if isDaily and permission and barstate.isconfirmed and emaTrendOk
    label.new(x=bar_index, y=high, text="T2", style=label.style_label_down, color=color.new(color.green, 0), textcolor=color.white)
    t2Triggered := true

permission := false

var bool permissionT3 = false
if isDaily and emaTrendOk
    permissionT3 := (close > open) and (open > ema10) and (close > ema10)

if isDaily and permissionT3 and barstate.isconfirmed and not t2Triggered and emaTrendOk
    label.new(x=bar_index, y=high, text="T3", style=label.style_label_down, color=color.new(color.orange, 0), textcolor=color.white)
